plugins {
	id 'java'
	id 'eclipse'
}

// Gson 2.10 requires Java 7, there's no point in compiling this for Java 5
targetCompatibility = 7;
sourceCompatibility = 7;

repositories {
	mavenCentral()
	flatDir {
		dirs 'lib'
	}
}

jar {
	from {
		configurations.extraLibs.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
	manifest {
		attributes(
			'Main-Class': 'org.betacraft.launcher.Launcher'
		)
	}
}

task osxBuildApp {
	dependsOn jar

	doLast {
		def dist = "${buildDir}/app/BetaCraft Launcher.app"
		mkdir("$dist/Contents/MacOS")
		mkdir("$dist/Contents/Resources")
		copy {
			from "${buildDir}/../src/main/app/Info.plist"
			into "$dist/Contents"
		}
		copy {
			from "${buildDir}/../src/main/app/PkgInfo"
			into "$dist/Contents"
		}
		copy {
			from "${buildDir}/../src/main/app/BetaCraft Launcher"
			into "$dist/Contents/MacOS"
		}
		copy {
			from "${buildDir}/../src/main/app/icon.icns"
			into "$dist/Contents/Resources"
		}
		copy {
			from "${buildDir}/libs/${project.name}.jar"
			into "$dist/Contents/MacOS"
		}
		exec {
			workingDir "${buildDir}/app/"
			commandLine('zip', '-rvFSX', "../libs/BetaCraft-Launcher-macOS.zip", '.')
		}
	}
}

configurations {
	extraLibs
}

dependencies {
	extraLibs 'com.google.code.gson:gson:2.10.1'
	compile fileTree(dir: 'lib', include: ['*.jar'])
}

task copyGson(type: Copy) {
	into "lib/" from configurations.extraLibs
}

compileJava.dependsOn copyGson
